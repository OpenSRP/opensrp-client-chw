// Top-level build file where you can add configuration options common to all sub-projects/modules.
apply plugin: 'com.github.kt3k.coveralls'
apply plugin: 'jacoco'
apply plugin: 'build-dashboard'

buildscript {

    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.4.2'
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.8.2'
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

jacoco {
    toolVersion = "0.8.0"
}

buildDashboard {
    reports.html.setDestination(file("build/"))
}
/**
 *  List of modules that don't require Jacoco
 */
def ignoredByJacoco = [
        "opensrp-chw-core"
]

allprojects {

    tasks.whenTaskAdded { task ->
        if (task.name == "lint") {
            task.enabled = false
        }
    }

    repositories {
        mavenLocal()
        mavenCentral()
        google()
        jcenter()
        maven { url 'https://maven.google.com' }
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
        maven { url 'https://jitpack.io' }
        maven { url "http://acra.googlecode.com/svn/repository/releases" }
        maven { url "http://repo.maven.apache.org/maven2" }
        maven { url "http://cloudant.github.io/cloudant-sync-eap/repository" }
        maven { url 'https://maven.fabric.io/public' }
        maven { url "https://s3.amazonaws.com/repo.commonsware.com" }
        maven { url "https://dl.bintray.com/ona/rdt-capture" }
    }
}

project.ext.preDexLibs = !project.hasProperty('disablePreDex')

subprojects { superProject ->
    group = 'org.smartregister'

    ext.androidToolsBuildGradle = '28.0.3'
    ext.androidBuildToolsVersion = '28.0.3'
    ext.androidMinSdkVersion = 18
    ext.androidCompileSdkVersion = 28
    ext.androidTargetSdkVersion = 28

    ext.androidAnnotationsVersion = '3.0.1'
    ext.androidAnnotationsAPIVersion = '3.0.1'

    // Improve build server performance by allowing disabling of pre-dexing
    // (see http://tools.android.com/tech-docs/new-build-system/tips#TOC-Improving-Build-Server-performance.)
    project.plugins.whenPluginAdded { plugin ->
        if ("com.android.build.gradle.AppPlugin" == plugin.class.name) {
            project.android.dexOptions.preDexLibraries = rootProject.ext.preDexLibs
        } else if ("com.android.build.gradle.LibraryPlugin" == plugin.class.name) {
            project.android.dexOptions.preDexLibraries = rootProject.ext.preDexLibs
        }
    }

    // Apply Jacoco only if the project is not in the ignore list
    if (!ignoredByJacoco.contains(superProject.name)) {
        task jacocoTestReport(type: JacocoReport, dependsOn: [
                'testBaDebugUnitTest', 'createBaDebugCoverageReport',
                'testWcaroDebugUnitTest', 'createWcaroDebugCoverageReport',
                'testTogoDebugUnitTest', 'createTogoDebugCoverageReport',
                'testGuineaDebugUnitTest', 'createGuineaDebugCoverageReport',
                'testChadDebugUnitTest', 'createChadDebugCoverageReport',
                'testDrcDebugUnitTest', 'createDrcDebugCoverageReport']) {

            reports {
                html.enabled = true
                xml.enabled = true
                csv.enabled = false
            }

            getReports().getXml().setDestination(file("${project.buildDir}/reports/jacoco/jacocoRootReport/merged.xml"))
            getReports().getHtml().setDestination(file("${project.buildDir}/reports/jacoco/jacocoRootReport/html"))

            def fileFilter = ['**/R.class', '**/R$*.class', '**/BuildConfig.*', '**/Manifest*.*', '**/*Test*.*', 'android/**/*.*', '**/*$ViewBinder*.*']
            def baDebugTree = fileTree(dir: "$project.buildDir/intermediates/classes/debug", excludes: fileFilter)
            def wcaroDebugTree = fileTree(dir: "$project.buildDir/intermediates/classes/debug", excludes: fileFilter)
            def togoDebugTree = fileTree(dir: "$project.buildDir/intermediates/classes/debug", excludes: fileFilter)
            def guineaDebugTree = fileTree(dir: "$project.buildDir/intermediates/classes/debug", excludes: fileFilter)
            def chadDebugTree = fileTree(dir: "$project.buildDir/intermediates/classes/debug", excludes: fileFilter)
            def drcDebugTree = fileTree(dir: "$project.buildDir/intermediates/classes/debug", excludes: fileFilter)
            def mainSrc = "$project.projectDir/src/main/java"

            sourceDirectories = files([mainSrc])
            classDirectories = files([baDebugTree, wcaroDebugTree, togoDebugTree, guineaDebugTree, chadDebugTree, drcDebugTree])
            executionData = fileTree(dir: project.buildDir, includes: [
                    'jacoco/testBaDebugUnitTest.exec', 'outputs/code-coverage/connected/*coverage.ec',
                    'jacoco/testWcaroDebugUnitTest.exec', 'outputs/code-coverage/connected/*coverage.ec',
                    'jacoco/testTogoDebugUnitTest.exec', 'outputs/code-coverage/connected/*coverage.ec',
                    'jacoco/testGuineaDebugUnitTest.exec', 'outputs/code-coverage/connected/*coverage.ec',
                    'jacoco/testChadDebugUnitTest.exec', 'outputs/code-coverage/connected/*coverage.ec',
                    'jacoco/testDrcDebugUnitTest.exec', 'outputs/code-coverage/connected/*coverage.ec'
            ])
        }

    }
}

// Only report code coverage for projects that are distributed
def publishedProjects = subprojects.findAll { it.path != ':opensrp-chw-core' }

task jacocoRootReport(type: JacocoReport, group: 'Coverage reports') {
    description = 'Generates an aggregate report from all subprojects'
    dependsOn publishedProjects.jacocoTestReport

    sourceDirectories = files(publishedProjects.jacocoTestReport.sourceDirectories)
    classDirectories = files(publishedProjects.jacocoTestReport.classDirectories)
    executionData = files(publishedProjects.jacocoTestReport.executionData)

    getReports().getXml().setDestination(file("build/reports/jacoco/jacocoRootReport/jacocoRootReport.xml"))
    getReports().getHtml().setDestination(file("build/reports/jacoco/jacocoRootReport/html"))

    reports {
        html.enabled = true
        xml.enabled = true
    }

    onlyIf = { true }

    doFirst {
        executionData = files(executionData.findAll {
            it.exists()
        })
    }
}

coveralls {
    for (int i = 0; i < publishedProjects.jacocoTestReport.sourceDirectories.size(); i++) {
        sourceDirs.add(publishedProjects.jacocoTestReport.sourceDirectories.get(i).getAsPath())
    }

    jacocoReportPath = file("build/reports/jacoco/jacocoRootReport/jacocoRootReport.xml")
}

tasks.coveralls {
    group = 'Coverage reports'
    description = 'Uploads the aggregated coverage report to Coveralls'
    dependsOn jacocoRootReport
}


